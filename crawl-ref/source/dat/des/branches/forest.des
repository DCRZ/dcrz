##############################################################################
# forest.des: Maps for the Forest.
# - entry vaults
# - minivaults
# - branch end vaults
##############################################################################

##############################################################################
# Forest entry vaults
##############################################################################

##############################################################################
NAME:   dummy_forest_entry
WEIGHT: 50
TAGS:   forest_entry
KFEAT:  O = enter_forest
MAP
O
ENDMAP

##############################################################################
# A really basic entry.
NAME:   grunt_forest_entry_basic
TAGS:   forest_entry vaults_entry_forest
KFEAT:  O = enter_forest
MONS:   plant / nothing
MAP
.........
..t...t..
.t11.11t.
..1t.t1..
....O....
..1t.t1..
.t11.11t.
..t...t..
.........
ENDMAP

##############################################################################
# A small clearing.
NAME:   grunt_forest_entry_clearing
TAGS:   forest_entry vaults_entry_forest
KFEAT:  O = enter_forest
MONS:   spriggan rider
MONS:   satyr / spriggan druid
MONS:   apis, ancient bear
SUBST:  t = t.
KMONS:  P = plant / bush
NSUBST: Q = 3:1 / 1:2
NSUBST: R = 4:3 / 1:4 / 1:5 / *:.
MAP
.tt...tt.
t..Q.Q..t
t.t.Q.t.t
.Q.P.P.Q.
..Q.O.Q..
.Q.P.P.Q.
t.t.Q.t.t
t..Q.Q..t
.tt...tt.
ENDMAP

##############################################################################
# A secluded grove.
NAME:   forest_entry_grove
TAGS:   forest_entry vaults_entry_forest vaults_orient_s
KFEAT:  O = enter_forest
MONS:   spriggan rider
MONS:   satyr / spriggan druid
MONS:   apis, ancient bear
KMONS:  P = plant / bush
NSUBST: Q = 3:1 / 1:2
NSUBST: R = 4:3 / 1:4 / 1:5 / *:.
MAP
   xxxxxxxxx
  xxxttttxxxx
 xxxttttttttxx
xxtttt...tttxxx
xttt..Q.Q..ttxx
xttt.Q.R.Q.tttx
xtt.........ttx
xtt.R..O..R.ttx
xtt..R.R.R..ttx
xttt..R.R..tttx
xttt.......tttx
xxtttt...ttttxx
 xxxtt..ttxxxx
  xxxtt..ttxx
   xxtt..ttx
ENDMAP

##############################################################################
# Forest minivaults
##############################################################################
NAME: grunt_forest_small_clearing_fruit
TAGS: transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = ration / w:90 nothing
MAP
 tt...tt
t.......t
t.......t
.........
.........
.........
t.......t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_pond
TAGS: transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
SUBST: w = wwW
SUBST: W = WW.
KMONS: O = plant / bush
MAP
 tt...tt
t.......t
t..WWW..t
..WwwwW..
..WwwwW..
..WwwwW..
t..WWW..t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_treed
TAGS: transparent patrolling
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
MONS: spriggan rider
MAP
 tt...tt
t.......t
t.1...1.t
...ttt...
...t*t...
...ttt...
t.1...1.t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_statue_fruit
TAGS: transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = ration / w:90 nothing
MAP
 tt...tt
t.......t
t.......t
.........
....G....
.........
t.......t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_statues
TAGS: transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
MONS: faun, faun / satyr w:3
NSUBST: 7 = 3:1 / 1:2
MAP
 tt...tt
t.......t
t.G.7.G.t
.........
..7.*.7..
.........
t.G.7.G.t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_large_clearing_fruit
TAGS: transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = ration / w:90 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t.................t
...................
...................
...................
t.................t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME: grunt_forest_large_clearing_guards
TAGS: transparent patrolling
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
MONS: spriggan rider, spriggan air mage, spriggan defender / spriggan berserker
NSUBST: P = 4:1 / 3:2 / 1:3
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t.......P.P.......t
.......P%.%P.......
.........G.........
.......P*.%P.......
t.......P.P.......t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME:  grunt_forest_large_clearing_statues
TAGS:  transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
KMONS: * = satyr / spriggan berserker
KITEM: . = ration / w:90 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t.......G.......t
tt....G.....G....tt
t.................t
...................
.....G...*...G.....
...................
t.................t
tt....G.....G....tt
 t.......G.......t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME:  grunt_forest_large_clearing_slash
TAGS:  transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
SUBST: w = wwW
SUBST: W = WW.
KMONS: O = plant / bush
KITEM: . = ration / w:90 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  tww...........t
 ttwww..........tt
 t..www..........t
tt...www.........tt
t.....www.........t
.......wwW.........
........WWW........
.........Www.......
t.........www.....t
tt.........www...tt
 t..........www..t
 tt..........wwwtt
  t...........wwt
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME:  grunt_forest_large_clearing_hut
TAGS:  transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
COLOUR: -*c = lightgrey
FTILE:  -* = floor_normal
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t......ccccc......t
.......c--*c.......
.......+-9*c.......
.......c--*c.......
t......ccccc......t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

# These vaults are derived from spriggan_forest.
NAME: kb_forest_berserker_hut_1
DEPTH: Forest
MONS:  spriggan berserker
KMONS: P = plant / plant / bush / nothing
SUBST: T = t / t / P / .
MAP
   tttttt
ttttPP.Tt
tccccT..@
tc%1c...T
tc..+..Tt
tc.1c...T
tcccct.Tt
tttT..ttt
 tTPTTt
 tTTttt
 tttt
ENDMAP

NAME: kb_forest_berserker_hut_2
DEPTH: Forest
MONS:  spriggan berserker
KMONS: P = plant / plant / bush / nothing
SUBST: T = t / t / P / .
KFEAT: _ = altar_trog
MAP
tt@t
t..tttttttt
t....xxxxxt
tT...x..1xt
tT...+...xt
ttt.tx..1xt
  tt.x.1_xt
   ttxxxxxt
    ttttttt
ENDMAP

NAME: kb_forest_witch_hut
DEPTH: Forest
MONS:  greenwood witch
SUBST: T = t / .
MAP
tt@tttt
t.ttttt
t.xxxxt
t.+.1xt
tTx.%xt
ttcccct
ttttttt
ENDMAP

NAME:  kb_forest_spriggan_shepherd
DEPTH: Forest
SUBST: T = t / t / P / .
COLOUR: "MN = green
KMONS: P = plant
MONS:  apis, spriggan ; robe . quarterstaff
ITEM:  ration q:3
MAP
ttttttt@t
txxxtt.tttttttt
txd+"""""""tt.@
txxx""""""""".tt
ttt""2"""""""TTt
ttT""11"11"TTTtt
tt""""1""""Tttt
tt".11""2TTtt
tt""1""""Ttt
ttt""""TTTt
  ttttttttt
ENDMAP

NAME:  kb_forest_druid_statue
DEPTH: Forest
MONS:  spriggan druid
MAP
tt@tt
t.1.t
t1G1t
t.1.t
ttttt
ENDMAP

# XXX: this one doesn't work very well outside the corner, or as a primary
# vault...
NAME:   kb_forest_skeletal_bear_cave
DEPTH:  Forest
ORIENT: southeast
MONS:   greenwood witch
MONS:   ancient bear skeleton
MAP
                   tttttt
                ttttxxxxx
            t@tttxxxxxxxx
            tt..xxxxxxxxx
          tttt....xxxxxxx
        tttt......}xxxxxx
       tt.....xxxxxxxxxxx
ttt  ttt....xxxxxxxxxxxxx
@.tttt...xxxxxxxxsxxxsxxx
tt.......xxxxx..sxxxxssxx
 tttxxxxx......xx.sxx..xx
txxxxxxxxxx....xxx.1.xxxx
 txxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:    bh_autumn_forest_stairs
# No threat or loot, so allow another vault.
TAGS:    extra
ORIENT:  float
DEPTH:   Forest
COLOUR:  t = lightred / red w:3
SHUFFLE: Aaq / Bbr / Ccs
SUBST:   q = @
SUBST:   A = ., bBcCdDrs = t
COLOUR:  t = lightred / red w:3 / green w:3 / lightgreen w:1
TILE:    t = dngn_tree_lightred / dngn_tree_red w:4 / dngn_tree_yellow w:4
COLOUR:  a = lightred / red / green / lightgreen
TILE:    a = dngn_tree_lightred / dngn_tree_red / dngn_tree_yellow / dngn_tree
SUBST:   a = t
MAP
  aaaa qqq aaaa
 caaaaaAAAaaaaab
cccaaaaAAAaaaabbb
ccc...........bbb
ccc...........bbb
 cc..xxxxxxx..bb
sCC..x.....x..BBr
sCC..x..{..x..BBr
sCC..x.....x..BBr
 cc..xV...Vx..bb
ccc..xx+++xx..bbb
ccc...........bbb
ccc...........bbb
ccctttttttttttbbb
 cctttttttttttbb
  ctttt    tttb
ENDMAP

# A den of bears.
NAME:   gammafunk_forest_bear_cave
TAGS:   patrolling no_monster_gen
DEPTH:  Forest
MONS:   ancient bear
KFEAT:  " = W
KMONS:  " = water elemental w:40 / elemental wellspring
KITEM:  % = ration
SUBST:  % = %., * = *:40 |, B = 1., E = tE
CLEAR:  E
MAP
EEEEEEEEEEEEEEEEEEEEEEEEE
EtttttxxtttxxxxxxttxxtttE
EttxxxxxxxxxxxxxxxxxxxxxE
Etxxxxxxxxxxx..xxxxxxxxtE
Etxxxxx"Wxx......x.xxxttE
Exxx%...1.....x....xxxxtE
Exxx%B.1...xxxxx.....xxtE
Exxxx%%%*xxxxxxxxxxx..xtE
Etxxx*xxxxxxxtxxxxxxx...@
Etxxxxxxxxxxtttttttxxx..@
EttxxxxxttttttttttttttttE
EEEEEEEEEEEEEEEEEEEEEEEEE
ENDMAP

NAME:   nicolae_forest_garden
TAGS:   no_monster_gen no_item_gen no_trap_gen transparent
DEPTH:  Forest
KMONS:  Y = faun
KMONS:  D = satyr
KMONS:  C = dryad
KMONS:  P = plant
KITEM:  f = ration
SUBST:  P = P., f = f...
NSUBST: D = 1:D / *:.
MAP
        .@@@.
      .........
    tt...P.P....tt
   tttttPPPPPPPtttt.
  .ttDfPP.Y....tDtt...
 .PPttP..P.P.PP.ftP...
 .P....PPP...PPP.PP..@
..PP..PPPwwwC.P..PPP.@
......Y.wwwwww.Y.ttP.@
.@.PP..Pwwwwww...fttt.
 @..P..PP.wwwPP.ttttt
  @.PP.PPP..PP..PPttt
  ...Ptt.P.PPPPPP....
  ...ttfDttPP.......
    .tttttt......@
     ..tt.......
        .....
ENDMAP

NAME: nicolae_forest_triple_paths
TAGS: transparent extra decor
SUBST: T = t_
CLEAR: _
MAP
         Tt@tT
         Tt.tT
     @TTTTt.tTTTT@
    T..tttt.tttt..T
    TT....t.t....TT
     Tttt.t.t.tttT
    TTttt.t.t.tttTT
TTTTTtttt.t.t.ttttTTTTT
ttttttttt.t.t.ttttttttt
TTTTTtttt.t.t.ttttTTTTT
    TTttt.t.t.tttTT
     Tttt.t.t.tttT
    TT....t.t....TT
    T..tttt.tttt..T
     @TTTtt.ttTTT@
        TTt.tTT
         Tt@tT
ENDMAP

NAME: nicolae_forest_godswood
TAGS: transparent extra decor
MAP
ttt     ttt
..ttttttt..
@...ttt...@
.tt..t..tt.
tttt...tttt
 tt..t..tt
 t.......t
 tt..w..tt
  t.www.t
 tt..w..tt
 t...C...t
 tt..t..tt
  tt...tt
   ttttt
ENDMAP

NAME: nicolae_forest_orc_logging_accident
TAGS: decor extra no_monster_gen no_trap_gen
SUBST: F = 't, T = _t
NSUBST: p = 4:p / *:., ' = o / '
SUBST: . = ...b
KPROP: bp = bloody
SUBST: b = .
{{
dgn.delayed_decay_extra(_G, 'o', 'orc corpse', 'hand axe / broad axe, ' ..
   'leather armour / pair of gloves, mundane quarterstaff')
dgn.delayed_decay_extra(_G, 'p', 'orc corpse', 'hand axe / broad axe, ' ..
   'leather armour / pair of gloves')
}}
KITEM: w = mundane quarterstaff
KITEM: s = ration
KFEAT: 'o = W
CLEAR:  _
MAP
       TTTTT
     TTtttttTTT
    TttttFFttttTT
   TttttFF'FtttttT
  TtttttF'''FtttttT
 TtttttttF''FttttttT
 Ttt.p........p.ttttT
TTt..xxxx........tptT
Tttp.xwwx...xxmx...tT
Tt...xww+...xpsx...tT
Tt.p.xwwxp..+.sm.p.tT
Tt...xxxx...x.sx..ttT
Tt..p.......xxmx..tTT
Ttt.....p......p.ttT
TTtttt..........ttTT
 TTTTtttt@@ttttttTT
    TTTT     TTTTT
ENDMAP

NAME:    nicolae_forest_spriggan_camp
DEPTH:   Forest
KMONS:   D = patrolling spriggan rider
KMONS:   SGHI = spriggan berserker / nothing
KMONS:   F = spriggan druid
SHUFFLE: Ff/Gg/Hh/Ii
SUBST:   ghi = .
MAP
       ttttt
  ttttttgggt
 @.ttt.t.G.t
@......tt.tt
t...D..S...t
tttt.......tt
 ttt.S..S...t
 tt......tt.t
tttt.tt.tt..t
ti...t...tH.t
tiI..t.F.thht
ti...tffftttt
tttttttttt
ENDMAP

NAME: nicolae_forest_oklob_treasure_island
TAGS: no_pool_fixup
DEPTH: Forest, !Forest:$
SHUFFLE: Aa/Bb/Cc/Dd/Ee/Ff/Gg/Hh/Ii, %*$
NSUBST: ' = 1:^ / 1:o / *:'
SUBST: abcde = w, fghi = t, ABCDE = .wW, FGHI = .t, ' = ':100 $$$%%, \
       _ = .._, . = ......W
KFEAT: ^ = permanent teleport trap
KMONS: o = oklob plant
CLEAR: _
MAP
       _____
     __.....__
    _........._
   _...GGFFF..._
  _...GggfffF..._
 _...GggGFFffF..._
 _..GggG'''FeeE.._
_..AaaA'''''EeeE.._
_..AaA'''''''EeE.._
_..AaA'''''''EeE.._
_..AaA'''''''DdD.._
_..BbbB'''''DddD.._
 _..BbbB'''DddD.._
 _...BbbCCCcdD..._
  _...BbccccC..._
   _...BCCCC..._
    _........._
     __.....__
       _____
ENDMAP

NAME:  nicolae_forest_game_of_thorns
DEPTH: Forest
TAGS:  no_monster_gen transparent
MONS:  thorn hunter
SUBST: T = t., W = WWWWw, q = W.
KFEAT: t = tree
MAP
      ttttt
    TtTTTtttT
   ....q.TTqTTTT
  @..1WT.WWWW.qT
 T.T..qWWqqWWTTT
TT..WWWqT.TW.q.T
t.TqWWq..T..W..tt
t.TTWWWq.Wq1.W.Tt
tqWW...WWqWWWq.Tt
tWq1.WWq..WWWq..t
tT..Wq.TT.qWWWWqt
tT..W..1.WWq..qWt
tTTTTWWWWTTTWWW.t
tT...WWWWTTTWWT.T
 T.q.1..qW..TT..
 TT.qt.qWq.1..@
  TT.tTTq....q
   TTtttTTTtTT
      ttttt
ENDMAP

##############################################################################

# A trio of precisely-mirrored pools.
NAME:    nicolae_quartered_pool_little
TAGS:    transparent extra decor uniq_qpool
DEPTH:   Forest
SHUFFLE: Aa, Bb, Cc, Dd, Ee, Ff, Gg, Hh, Ii, Jj, Kk, Ll, Mm, Nn, Oo, Pp, Qq
SUBST:   abcdefghijklmnopq = ., ABCDEFGHIJKLMNOPQ = W
NSUBST:  7 = 1:@ / * = t@@@
MAP
    777777
   7qolLOQ7
  7pnkiIKNP7
 7pmjhfFHJMP7
7qnjgedDEGJNQ7
7okhecbBCEHKO7
7lifdbaABDFIL7
7LIFDBAabdfil7
7OKHECBbcehko7
7QNJGEDdegjnq7
 7PMJHFfhjmp7
  7PNKIiknp7
   7QOLloq7
    777777
ENDMAP

NAME:    nicolae_quartered_pool_medium
TAGS:    transparent extra decor uniq_qpool
DEPTH:   Forest
SHUFFLE: Aa, Bb, Cc, Dd, Ee, Ff, Gg, Hh, Ii, Jj, Kk, Ll, Mm
SHUFFLE: Nn, Oo, Pp, Qq, Rr, Ss, Tt, Uu
SUBST:   abcdefghijklmnopqrstu = ., ABCDEFGHIJKLMNOPQRSTU = W
NSUBST:  7 = 1:@ / * = t@@
MAP
      7777
    77spPS77
   7urolLORU7
  7tqnkiIKNQT7
 7uqmjhfFHJMQU7
 7rnjgedDEGJNR7
7sokhecbBCEHKOS7
7plifdbaABDFILP7
7PLIFDBAabdfilp7
7SOKHECBbcehkos7
 7RNJGEDdegjnr7
 7UQMJHFfhjmqu7
  7TQNKIiknqt7
   7UROLloru7
    77SPps77
      7777
ENDMAP

NAME:    nicolae_quartered_pool_super
TAGS:    transparent extra decor uniq_qpool
DEPTH:   Forest
SHUFFLE: Aa, Bb, Cc, Dd, Ee, Ff, Gg, Hh, Ii, Jj, Kk, Ll, Mm
SHUFFLE: Nn, Oo, Pp, Qq, Rr, Ss, Tt, Uu, Vv, Ww, Xx, Yy, Zz
SHUFFLE: 12, 34, 56
SUBST:   abcdefghijklmnopqrstuvwxyz135 = ., ABCDEFGHIJKLMNOPQRSTUVWXYZ246 = W
NSUBST:  7 = 1:@ / * = t@
MAP
      777777
    771xtTX277
   75zwspPSWZ67
  73yvrolLORVY47
 75yuqnkiIKNQUY67
 7zvqmjhfFHJMQVZ7
71wrnjgedDEGJNRW27
7xsokhecbBCEHKOSX7
7tplifdbaABDFILPT7
7TPLIFDBAabdfilpt7
7XSOKHECBbcehkosx7
72WRNJGEDdegjnrw17
 7ZVQMJHFfhjmqvz7
 76YUQNKIiknquy57
  74YVROLlorvy37
   76ZWSPpswz57
    772XTtx177
      777777
ENDMAP

##############################################################################

NAME:    nicolae_forest_grove_arrangements
TAGS:    transparent decor extra
DEPTH:   Forest
SHUFFLE: abcdefghi/''''T''''/T'''''''T/T'''T'''T/T'T'''T'T/T'T'T'T'T/\
         TTT'''TTT/'T'T'T'T'/T'T''''T'/'T'TTT'T'/'T'''''T'/TTTT'TTTT/\
         T'TTTTT'T/TT'TTT'TT/'''TTT'''/T'TT'TTTT/TT'T'T'TT/TT'''''TT/\
         TT''T''TT/TT''TT'T'/T''''T'T'
SUBST:   abcdefghiT = ttttt., ' = ., _ = ._
CLEAR:   _
MAP
__________________
_................_
_..aa...bb...cc.._
_.aaaa.bbbb.cccc._
_.aaaa.bbbb.cccc._
_..aa...bb...cc.._
_................_
_..dd...ee...ff.._
_.dddd.eeee.ffff._
_.dddd.eeee.ffff._
_..dd...ee...ff.._
_................_
_..gg...hh...ii.._
_.gggg.hhhh.iiii._
_.gggg.hhhh.iiii._
_..gg...hh...ii.._
_................_
__________________
ENDMAP

###############################################################################
## Yred has gained a foothold in the enchanted forest
#  This vault should be merged with its crypt variant
#  gammafunk_crypt_yreds_forest in float.des, but it's too 
#  much of a pain
NAME:    gammafunk_forest_yreds_foothold
DEPTH:   Forest
ORIENT:  float
TAGS:    patrolling
SHUFFLE: HI / JK / LM / NO / PQ / RS
SUBST:   HJLNP = t, IKMOQ = T, RS = ., F = 1:30 3, e = efghij
SUBST:   T = tD
FTILE:   _|567$*+ = floor_tomb
SUBST:   E = tE
KFEAT:   D = t
TILE:    D = dngn_tree_dead
: dgn.delayed_decay(_G, 'e', 'spark wasp corpse')
: dgn.delayed_decay(_G, 'f', 'caustic shrike corpse')
: dgn.delayed_decay(_G, 'g', 'apis corpse')
: dgn.delayed_decay(_G, 'h', 'satyr corpse')
: dgn.delayed_decay(_G, 'i', 'ancient bear corpse')
: dgn.delayed_decay(_G, 'j', 'faerie dragon corpse')
MONS:    flayed ghost / phantasmal warrior
MONS:    vampire mage / vampire knight
MONS:    jiangshi / eidolon / shadow wraith / freezing wraith
MONS:    ancient champion / ghoul / wendigo
MONS:    w:20 death knight / w:20 bone dragon / revenant
MONS:    profane servitor
KFEAT:   7 = altar_yredelemnul
TILE:    c = dngn_stone_dark
CLEAR:   E
MAP
EEEEE..@...@.EEEEEEE...@.EEEE.@.EEEE
Etttttt......ttttttttttt.tttt...tttE
Ettttttt...ttttttttttttttttt..tttttE
EttttttttttttttTTTTTTTttttt..ttttttE
@....ttttttttTTT.....TTTtttt3.1ttt..
@....eFPPPPPQT.........TTttttN.tttt@
Ettte2PPPPQQQ..cG.4.Gc..TONNNNttttt.
EttttttttTT...ccc+++ccc...OTtttttttE
EtttttttTT..cccc_____cccc..TTtttettE
Etttt3HHI..Gc|$c_____c$|cG..Ttt.2etE
Ettt.1HHI..4c*$+_675_+$*c...SRRF...@
@....ttTT..Gc|$c_____c$|cG..SRR.tt.@
@...ttttTT..cccc_____cccc..TTttttttE
EttttttJJJK...ccc+++ccc...TTtttttttE
EttttttJJJJK...cG.4.Gc...MMLLLL3..tE
Etttt.F2JttTT...........TMLLLLF.1..E
Ettttte.etttTTTT.....TTTTtttttttt...
...tttt..ttttttTTTTTTTtttttttttttt.@
@..tttt..ttttttttttttttttttttttttt..
....tttt...tttttttttttttt...tttttttE
..@..EEEE.@.EEEEEEEEEEEE.@..@.EEEEEE
ENDMAP

##############################################################################
# Forest ends
#
# IMPORTANT: If you intend to contribute a Forest end vault, please follow 
# these guidelines:
#  * Ensure that your vault includes the Enchantress in a palace or
#    similar building;
#  * Place approximately 20 superb items (|) and 30 good items (*) to ensure
#    that the Forest end vault loot is consistently comparable to (but  
#    noticeably worse than) Slime.
#     - ManMan
##############################################################################
NAME:   grunt_forest_end_chambers
ORIENT: north
PLACE:  Forest:$
KITEM:  O = mossy rune of Zot
KITEM:  0 = superb_item
KITEM:  1 = superb_item
KMONS:  0 = the Enchantress
KMONS:  1 = spriggan defender
MONS:   spriggan defender, spriggan berserker, spriggan druid
MONS:   spriggan rider, spriggan air mage
MONS:   spriggan defender / spriggan berserker / spriggan druid / \
        spriggan rider / spriggan air mage / nothing w:100
MONS:   thorn hunter / shambling mangrove
TILE:   c = wall_marble
COLOUR: .|*%OT0123456+ = lightgray
FTILE:  .|*%OT0123456+ = floor_normal
MAP
tttttttttttttttttttttttttttttttttt
tttttttttttttccccccccttttttttttttt
tttttttttttccc||O|||cccttttttttttt
ttttttttttcc..........cctttttttttt
tttttttttcc....4444....ccttttttttt
tttttttttc..............cttttttttt
ttttttttcc..6.5....5.6..cctttttttt
ttttttttc|.6..........6.|ctttttttt
ttttttttc|.6..........6.|ctttttttt
ttttttttc|.6....01....6.|ctttttttt
ttttttttc|.6....11....6.|ctttttttt
ttttttttc|.6..........6.|ctttttttt
ttttttttc|.6..5....5..6.|ctttttttt
ttttttttcc..6........6..cctttttttt
tttttttttc..............cttttttttt
tttttttttcc....4224....ccttttttttt
ttttcccccccc..........cccccccctttt
tttcc******ccc......ccc******ccttt
ttcc.4.4.4..cccc++cccc..4.4.4.cctt
tcc..........c.6..6.c..........cct
tc*..........c+.66.+c..........*ct
tc*......1....cccccc....1......*ct
tc*....T.2....cccccc....2.T....*ct
tc*...........cccccc...........*ct
tc*.6........c+.66.+c........6.*ct
tcc.6........c.6..6.c........6.cct
ttc..66.....cccc++cccc.....66..ctt
ttccc..66..cccc....cccc..66..ccctt
ttttccc**cccc........cccc**cccLttt
tLttLLcccccc...6446...ccccccLtLtLt
ttLLLLtLLLcc..........cctLLLtttLLt
ttLLLLLLLLc*.6......6.*cLLtLLtttLt
tLLtLLL7LLc*.6..TT..6.*cLLLLtLLLLt
ttLLLLLLLLc*.6..TT..6.*cLLLLLLLLLt
tttLtLLLLLc*.6......6.*cLLtLLLtLLt
ttLLLLLtLLcc..........ccLLL7LLLLLt
ttLL7LLLLLLc...6666...cLLLLLtLL7Lt
tLLLLLLLLLLcc........ccLLtLLLLLLtt
ttLLtLL7LLLLccc....cccLL7LLLtLtttt
tttttLLLLLLLLLc++++cLLLLLLtttttttt
tttttttLLLLLLLLLLLLLLLLttLLLtttttt
tttttttttttLLLLLLLLLLtttLLLLLttttt
tttttttttttttttLLLLtttLLLttttttttt
ttttttttttttttttLLLLLLLttttttttttt
ttttttttttttttttL@LLtttttttttttttt
ENDMAP

NAME:   kb_forest_enchantress_palace
ORIENT: northeast
PLACE:  Forest:$
KITEM:  O = mossy rune of Zot
MONS:   spriggan rider / spriggan druid / \
        spriggan berserker / spriggan defender / \
        spriggan air mage
MONS:   spriggan rider
MONS:   spriggan druid
MONS:   spriggan berserker
MONS:   spriggan air mage
MONS:   spriggan defender
KMONS:  7 = the Enchantress
KMONS:  A = greenwood witch
KMONS:  B = pixie
KMONS:  X = spriggan rider / spriggan druid / \
            spriggan berserker / spriggan defender / \
            spriggan air mage
KMONS:  Y = spriggan air mage
KFEAT:  ~ = +
SUBST:  . = .:100 / 1:2 / 2:1 / 3:1 / 4:1 / 5:1
SUBST:  T = t / t / P / .
KMONS:  P = plant / plant / bush / nothing
KMONS:  R = plant
KMONS:  Q = bush
KMONS:  K = hornet
KITEM:  f = any weapon good_item
KITEM:  g = any armour good_item
KITEM:  h = *
KITEM:  7 = |
KMONS:  L = spriggan ; robe . dagger
KITEM:  i = ration q:2
COLOUR: C = white
KFEAT:  C = stone_wall
TILE:   C = wall_marble
KFEAT:  - = floor
FTILE:  OR-~67ABXYfg$*| = floor_normal
MAP
ttttttttttttttttttttttttttttttttttttttttttt
tttCCCCCCCCCCCCCCtttttttttttttttttttttttttt
tttC|||||7O|||||CtttttttttTTTTTttTTTTtttttt
tttC--B------A--CtttQTtttTT.T.Ttt.T.TTTtttt
tttC----6--6----Ctt..ccccc....cccc.TT.Ttttt
tttCCCCCC~~CCCCCC....ch.1c....c.hc.t.TTtttt
tttC*|*C6--6C*|*CtT..ch1.+....+1hcT.TT.tttt
tttC|*|~----~|*|Ctt..ccccc....cccc.TT.Ttttt
tttC*|*C6--6C*|*CttQ..................ttttt
tttCCCCCC~~CCCCCCttttccccc....cccc......ttt
tttCgggC--X-CfffCttttcixLc....c1hc.....tttt
tttCggg~----~fffCttttci..+....+4hc..1...ttt
tttCgggCX--XCfffCttttccccc....cccc...1.tttt
tttCCCCCC~~CCCCCCttttt...................tt
tttCR-----X----RCccccccccc....5.........ttt
tttC--X--------XCcKxKxKxKc......2......tttt
tttCRX--Y--Y--XRCc.22.3..c.....22......tttt
tttCCCCCC~~CCCCCCc+ccccccc....2.2.....ttttt
ttt..................................tttttt
@...................................ttttttt
t..TTttttt............5..........ttt.tttttt
tttttttttttttttt...............tttttt...ttt
ttttttttttttttttttt.........tttttttttttt<tt
ttttttttttttttttttttttt....tttttttttttttttt
tttttttttttttttttttttt@tt..@ttttttttttttttt
ENDMAP

NAME:   grunt_forest_end_winery
TAGS:   no_pool_fixup
PLACE:  Forest:$
ORIENT: northeast
KITEM:  O = mossy rune of Zot
MONS:   the Enchantress band
MONS:   spriggan berserker / spriggan defender
MONS:   spriggan druid / spriggan air mage, plant
MONS:   faun w:20 / satyr / thorn hunter w:5
MONS:   spark wasp / caustic shrike w:5
KMONS:  7 = water nymph
KFEAT:  7 = deep_water
NSUBST: w = 2:7 / *:w
KFEAT:  T = tree / floor
SUBST:  X = xt
CLEAR:  x
KFEAT:  : = floor
ITEM:   ration / nothing w:90
ITEM:   any potion / ration w:5 / nothing w:25
TILE:   c = wall_vines
FTILE:  e123+|%*$-O = floor_vines
MAP
tttttttttttttttttttttttttttttttttttttttttttt
Xttttttttttttttttttttttttttttttttttttttttttt
XttTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTtt
XttT.5...................................Ttt
XttT..4d4d4.cccccccccccccccccccccccccccc.Ttt
XttT..4d4d4.ce-e-e-e-e-e-e-ec******+||Oc.Ttt
XttT..4d4d4.c-e-e-e-c-e-e-e-c-----*c|||c.Ttt
XttT..4d4d4.ce-e2e-ece-e2e-ec-----*cc+cc.Ttt
XttT..4d4d4.cc+ccccccc+cccccc-----*c|*|c.Ttt
XttT..4d4d45c-----3---3-----+----1*c|*|c.Ttt
XttT..4d4d4.c-------2-------+-----*c|*|c.Ttt
XtT...4d4d4.c--wwwwwwwwwww--+-----*c|*|c.Ttt
XtT...4d4d4.c--wwwwwwwwwww--c-----*c|*|c.Ttt
XtT...4d4d4.c-----3---3-----c-----*c|*|c.Ttt
XtT...4d4d4.c---------------c******+|*|c.Ttt
XTT...4d4d4.cc++cccccccccccccccccccccccc.Ttt
T.......5......5............5............Ttt
@TT...44444444..444444444444..4444444444.Ttt
TtT...dddddddd..ddd6dddddddd..ddddddd6dd.Ttt
XtT...444444445.444444444444.54444444444.Ttt
XtT...dddddd6d..dddddddddddd..d6dddddddd.Ttt
XttT..44444444.54444444444445.4444444444.Ttt
XttT..dd6ddddd..ddddddddd6dd..dddddddddd.Ttt
XttT..44444444..444444444444..4444444444.Ttt
XttT.....................................Ttt
XttT.....................................Ttt
XttTTTTTT...........TTTT...........TTTTTTTtt
XttttttttTTTTT.TTTTTTTTTTTTTT.TTTTTTTTTTTTtt
XXtttttttttttT.TttttttttttttT.Tttttttttttttt
XXXXXXXXXXXXT@TXXXXXXXXXXXXXXT@TXXXXXXXXXXXt
ENDMAP

NAME:   grunt_forest_end_shrine
PLACE:  Forest:$
ORIENT: float
NSUBST: O = 1:A / 1:|
KITEM:  A = mossy rune of Zot
MONS:   the Enchantress band
MONS:   wendigo / shambling mangrove
MONS:   spriggan air mage / spriggan druid / \
        spriggan berserker / spriggan rider w:5 / \
        spriggan defender w:5 / ancient bear w:5 / \ 
        thorn hunter w:5 / shambling mangrove w:5
KMONS:  4 = pixie
KFEAT:  _ = altar_xom
KFEAT:  - = floor
SUBST:  T = tx
CLEAR:  x
FTILE:  14|*$-+AP_ = floor_dirt
KITEM:  4 = *
KITEM:  _ = |
TILE:   c = wall_church
MAP
  TTTTTTTTTTTTT
 TTtttttttttttTT
TTtt...........ttTT
Ttt3...........3ttT
Tt..ccccccccccc..tTTTTTTTT
Tt..c|||c4****c..ttttttttt
Tt..c|O|c*****c.2t..3....t
Tt..c|||c*****c..........t
Tt..cc+cc-----+....G.G.G..
Tt2.c-----1_--+..2.......@
Tt..cc+cc-----+....G.G.G..
Tt..c|||c*****c..........t
Tt..c|O|c*****c.2t..3....t
Tt..c|||c4****c..ttttttttt
Tt..ccccccccccc..tTTTTTTTT
Ttt3...........3ttT
TTtt...........ttTT
 TTtttttttttttTT
  TTTTTTTTTTTTT
ENDMAP

###############################################################################
## A palace where denizens of the forest gather to worship a powerful
## water deity
NAME:     gammafunk_forest_water_palace
PLACE:    Forest:$
ORIENT:   north
TAGS:     no_monster_gen no_rotate patrolling
SHUFFLE:  HI / JK, QR / SP
SUBVAULT: H : gammafunk_forest_water_palace_column
SUBVAULT: I : gammafunk_forest_water_palace_column
SUBVAULT: J : gammafunk_forest_water_palace_pool
SUBVAULT: K : gammafunk_forest_water_palace_pool
SUBVAULT: Q : gammafunk_forest_water_palace_column
SUBVAULT: R : gammafunk_forest_water_palace_column
SUBVAULT: S : gammafunk_forest_water_palace_pool
SUBVAULT: P : gammafunk_forest_water_palace_pool
KITEM:    O = mossy rune of Zot
MONS:     nothing w:30 / spriggan air mage / spriggan druid 
MONS:     w:20 faun / satyr, spriggan berserker
KMONS:    4 = caustic shrike
KMONS:    5 = spriggan defender
KMONS:    6 = shambling mangrove / thorn hunter
KMONS:    7 = ancient bear
KMONS:    8 = the Enchantress
KMONS:    A = water elemental
KMONS:    L = greenwood witch / wendigo w:30
KMONS:    N = water nymph
KMONS:    M = elemental wellspring
KFEAT:    ALMNS = shallow_water
KITEM:    458 = *
COLOUR:   c = white
FTILE:    |*12345678GTU+.O = floor_pebble_cyan
CLEAR:    E
MAP
ttttttttttttttttttttttttttttttttttttttttttttttttt
Ett.....WWWWWWWWcccccccccccccccccWWWWWWWW.....ttE
Ett....WWWWWWWWcc|||||||O|||||||ccWWWWWWWW....ttE
Ett....WWWGLWWWc||*****585*****||cWWWLGWWW....ttE
Ett....WWWWWWWWc||4***********4||cWWWWWWWW....ttE
Ett....WWWWWccccccccccc+++cccccccccccWWWWW....ttE
Ett....WWWWccWWWWWWWWWWWWWWWWWWWWWWWccWWWW....ttE
Ett.1..WWWWcWWWWWWWWWWWWWWWWWWWWWWWWWcWWWW..1.ttE
Ett....WWLWcWWHHHWWMWWW.U.WWWMWWIIIWWcWWLW....ttE
Ett....WWWWcWWHHHWWWWWU...UWWWWWIIIWWcWWWW....ttE
Ett....WWWWcWWHHHWWWW.......WWWWIIIWWcWWWW....ttE
Ett.1..WWWWcWWWWWWWWW..555..WWWWWWWWWcWWWW..1.ttE
Ett....WWWWcWWWWWWWWW.......WWWWWWWWWcWWWW....ttE
Ett....WWWWcWWWQQQWWWW.....WWWWRRRWWWcWWWW....ttE
Ett....WWWWcWWWQQQWWWWW...WWWWWRRRWWWcWWWW....ttE
Ett.1..WWLWcWWWQQQWWLWWWWWWWLWWRRRWWWcWWLW..1.ttE
Ett....WWWWcWWWWWWWWWWWWWWWWWWWWWWWWWcWWWW....ttE
Ett....WWWWcWWWWWWWWNWWWWWWWNWWWWWWWWcWWWW....ttE
Ett....WWWWcWWWWWWWWWWWWWWWWWWWWWWWWWcWWWW....ttE
Ett.1..WWWWcWWWcccWWWWW...WWWWWcccWWWcWWWW..1.ttE
Ett....WWWWcWWWcTcWWWW.2.3.WWWWcTcWWWcWWWW....ttE
Ett....WWWWcWWW.7.WWWW.....WWWW.7.WWWcWWWW....ttE
Ett....WWWWcWWWWWWWWWW.3.2.WWWWWWWWWWcWWWW....ttE
Ett....WWLWcWWWWLWWWWW.....WWWWWLWWWWcWWLW....ttE
Ett.1..WWWWccWWWWWWWWW.....WWWWWWWWWccWWWW..1.ttE
Ett....WWWWWccccccccccc+++cccccccccccWWWWW....ttE
Ett....WWWWWWWWc.................cWWWWWWWW....ttE
Ett....WWWWWWWWc..T...2...3...T..cWWWWWWWW....ttE
Ett....WWcccWWWc......3...2......cWWWcccWW....ttE
Ett....WWcGcWWWc...T..2...3..T...cWWWcGcWW....ttE
Ett....WWWWWWWWcc...............ccWWWWWWWW....ttE
Ett.....WWWWNWWWccccccc+++cccccccWWWNWWWW.....ttE
Ettt......WWWWWWWAWWAWWWWWWAWWAWWWWWWWW......tttE
Etttt......WWWWWWWWWWWWWWWWWWWWWWWWW........ttttE
Etttttt............T.........T............ttttttE
Etttttttt.....6...................6.....ttttttttE
Etttttttttt........T.........T........ttttttttttE
Ettttttttttttt.....................tttttttttttttE
Ettttttttttttttttt.............tttttttttttttttttE
Etttttttttttttttttttt.......ttttttttttttttttttttE
EEEEEEEEEEEEEEEEEEEEEEE.@.EEEEEEEEEEEEEEEEEEEEEEE
ENDMAP

NAME: gammafunk_forest_column
TAGS: gammafunk_forest_water_palace_column unrand no_rotate no_flip no_vmirror \
      allow_dup patrolling no_monster_gen
KMONS:  4 = ancient bear
COLOUR: c = white
FTILE:  G4. = floor_pebble_cyan
MAP
ccc
cGc
.4.
ENDMAP

NAME:  gammafunk_forest_pool
TAGS:  gammafunk_forest_water_palace_pool unrand no_rotate no_vmirror
TAGS:  allow_dup patrolling no_monster_gen
SUBST: N = N:20 L
KMONS: N = water nymph
KMONS: L = faerie dragon
KFEAT: NL = shallow_water
MAP
WWW
WNW
WWW
ENDMAP
